Include: setup.vader

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

Given (A header):
  ## Hello world

Do (<leader>] (demote header)):
   ]

Expect (Header to be demoted):
  ### Hello world

Do (<leader>[ (promote header)):
   [

Expect (Header to be promoted):
  # Hello world

Do (<leader>[<leader>[ (promote to normal line)):
   [ [

Expect (A regular line):
  Hello world

Do (<leader>]<leader>]<leader>]<leader>]<leader>] (promote to normal line)):
   ] ] ] ] ]

Expect (A regular line):
  Hello world

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

Given (A regular line):
  hello world

Do (<prefix>'):
   '

Expect (The line to be a quote):
  > hello world

Do (<prefix>'<prefix>'):
   ' '

Expect (The line to be unchanged):
  hello world

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

Given (A regular line):
  hello world

Do (<leader>t):
   t

Expect (The line to become a checkbox line):
  [ ] hello world

Do (<leader>t<leader>t):
   t t

Expect (The line to be unchanged):
  hello world

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

Given (A regular line):
  hello world

Do (<leader>lt):
   lt

Expect (The line to become a checklist line):
  - [ ] hello world

Do (<leader>lt<leader>lt):
   lt lt

Expect (The line to be unchanged):
  hello world

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

Given (A regular line):
  hello world

Do (<leader>ll):
   ll

Expect (The line to become a list item):
  - hello world

Do (<leader>ll<leader>ll):
   ll ll

Expect (The line to be unchanged):
  hello world
